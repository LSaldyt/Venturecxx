// Ball and urn model with Poisson prior

type Ball;
type Draw;
type Color;

distinct Color Blue, Green;
distinct Draw Draw[20];

#Ball ~ Poisson(6);

random Color TrueColor(Ball b) ~ Categorical({Blue -> 0.5, Green -> 0.5});

random Ball BallDrawn(Draw d) ~ UniformChoice({b for Ball b});

random Color ObsColor(Draw d) ~
  if (BallDrawn(d) != null) then 
    case TrueColor(BallDrawn(d)) in {
      Blue -> Categorical({Blue -> 0.8, Green -> 0.2}),
      Green -> Categorical({Blue -> 0.2, Green -> 0.8})
    };

/* Evidence file asserting that the drawn balls appeared blue on half the 
 * draws and green on half the draws.
 */

obs ObsColor(Draw[0]) = Blue;
obs ObsColor(Draw[1]) = Green;
obs ObsColor(Draw[2]) = Blue;
obs ObsColor(Draw[3]) = Green;
obs ObsColor(Draw[4]) = Blue;
obs ObsColor(Draw[5]) = Green;
obs ObsColor(Draw[6]) = Blue;
obs ObsColor(Draw[7]) = Green;
obs ObsColor(Draw[8]) = Blue;
obs ObsColor(Draw[9]) = Green;
obs ObsColor(Draw[10]) = Blue;
obs ObsColor(Draw[11]) = Green;
obs ObsColor(Draw[12]) = Blue;
obs ObsColor(Draw[13]) = Green;
obs ObsColor(Draw[14]) = Blue;
obs ObsColor(Draw[15]) = Green;
obs ObsColor(Draw[16]) = Blue;
obs ObsColor(Draw[17]) = Green;
obs ObsColor(Draw[18]) = Blue;
obs ObsColor(Draw[19]) = Green;

/* Query file asking for the total number of balls in the urn.
 */
query size({b for Ball b});

