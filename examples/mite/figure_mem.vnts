// utility definitions
define pass = return(nil);

// end utility defs

// example usage

define example_mem_hmm = () -> {
  run_in({
    // import defs from toplevel environment
    assume mem = $mem;
    // end defs
    assume x = mem((t) -> {
      if (t == 0) { flip() }
      else {
	bernoulli(if (x(t - 1)) { 0.9 } else { 0.1 })
      }
    });
    assume y = (t) -> {
      bernoulli(if (x(t)) { 0.7 } else { 0.3 })
    };
    assume ys = vector(y(1), y(2), y(4), y(5), y(7), y(8));
    value_at(toplevel(4))
  }, blank_trace())
};

define example_mem_hmm_regen = () -> {
  run_in({
    // import defs from toplevel environment
    assume mem = $mem;
    // end defs
    assume x = mem((t) -> {
      if (t == 0) { flip() }
      else {
	bernoulli(if (x(t - 1)) { 0.9 } else { 0.1 })
      }
    });
    assume y = (t) -> {
      bernoulli(if (x(t)) { 0.7 } else { 0.3 })
    };
    predict vector(x(1), x(2), x(3), y(1), y(2), y(3));
    xy <- value_at(toplevel(4));
    _ = debug('xy', xy);
    sp_addr = request(toplevel(1), toplevel(2));
    s <- single_site_subproblem(request(sp_addr, 2));
    repeat(20, {
      p <- extract(s);
      w <- regen(s, rest(p));
      _ = debug('r', first(p));
      _ = debug('x', w);
      xy <- value_at(toplevel(4));
      _ = debug('xy', xy);
    });
    return (nil)
  }, graph_trace())
};

define mem = (f) -> {
  make_sp((f) -> {
    dict(['state', counter()],
	 ['apply', (trace_handle, app_id, x) -> {
	   ref_count <- counter_get(x);
	   y <- with(trace_handle, {
	     addr <- request_address(x);
	     if (ref_count > 0) {
	       value_at(addr)
	     }
	     else {
	       let exp = [| f($x) |];
	       let env = extend_environment(
		 get_empty_environment(), 'f', address_of(f));
	       eval_request(addr, exp, env)
	     }
	   });
	   counter_incr(x);
	   return (y)
	 }],
	 ['propagating_kernel', (trace_handle, app_id, parent) -> {
	   addr <- with(trace_handle, {
	     x <- value_at(subexpression(1, app_id));
	     request_address(x)
	   });
	   if (addr == parent) {
	     return (dict(
	       ['extract', (output, x) -> {
		 return (pair(0, output))
	       }],
	       ['regen', (x) -> {
		 output <- with(trace_handle, {
		   value_at(addr)
		 });
		 return (pair(0, output))
	       }],
	       ['restore', (x, output) -> {
		 return (output)
	       }]))
	   }
	   else {
	     return (nil)
	   }
	 }])
  })
};
