// Copyright (c) 2015 MIT Probabilistic Computing Project.
//
// This file is part of Venture.
//
// Venture is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// Venture is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with Venture.  If not, see <http://www.gnu.org/licenses/>.

// Line parameter prior
assume intercept = normal(0,7);
assume slope = normal(0,4);

// Hypothesized linear relationship
assume line = proc(x) { slope * x + intercept };

// Prior on the outlier rate
assume outlier_prob = uniform_continuous(0.01, 0.3);

// Per-point outlier check
assume is_outlier = mem(proc(i) { flip(outlier_prob) });

// Outlier model: Broad Gaussian independent of the line
assume obs_outlier = proc(x) { normal(0,20) };

// Prior for unknown noise level
assume noise = gamma(1, 1);

// Inlier model: Linear relationship plus Gaussian noise of unknown
// but fixed magnitude.
assume obs_inlier = proc(x) { normal(line(x), noise) };

// Full data model: Either outlier or inlier.
assume obs = proc(i, x) {
  if (is_outlier(i)) {
    obs_outlier(x)
  } else {
    obs_inlier(x)
  }
};
