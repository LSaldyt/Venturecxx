replicate = (n, f) ~> {
  if (n <= 0) { list() } else {
    pair(f(), replicate(n-1, f))
  }
};

map = (f, l) -> {
  if (is_pair(l)) {
    pair(f(first(l)), map(f, rest(l)))
  } else {
    list()
  }
};

for_each = (f, l) -> {
  if (is_pair(l)) {
    f(first(l));
    for_each(f, rest(l))
  } else {
    "done"
  }
};

for_each2 = (f, l1, l2) -> {
  if (is_pair(l1) && is_pair(l2)) {
    f(first(l1), first(l2));
    for_each2(f, rest(l1), rest(l2))
  } else {
    "done"
  }
};

trace_of = (sp, args) ~> {
  t1 = get_current_trace();
  t2 = get_current_trace();
  res = regenerate(sp, args, t1, t2);
  list(first(res), t2)
};

subtrace_at = (trace, site) -> {
  if (is_pair(site)) {
    subtrace_at(subtrace(trace, first(site)), rest(site))
  } else {
    trace
  }
};
